start: value*

value: object
    | array
    | range
    | tuple
    | sample
    | number
    | WORD
    | ESCAPED_STRING
    | VAR

object: "{" [ pair ("," pair)* ] "}"
pair: WORD ":" value

array: "[" [ value ("," value)* ] "]"
range: "[" number ".." number "]"
    | "[" number "..." number "]" -> crange
tuple: "(" [ value ("," value)* ] ")"

sample: die_count distribution
    | distribution -> sample1
distribution: DIE number -> d_number
    | DIE array -> d_array
    | DIE range -> d_range
    | DIE tuple -> d_tuple

number: float | int

float: FLOAT
int: INT
die_count: DIE_COUNT

// Tokens

FLOAT.2: ["+"|"-"] UNSIGNED_FLOAT
DIE.2: /[dD]/
DIE_COUNT.2: SIGNED_INT /(?=[dD])/
INT.2: SIGNED_INT /(\b)/
VAR: "$" WORD
WORD: (LETTER | "_") (LETTER | DIGIT | "_")*

UNSIGNED_FLOAT: INT _EXP
    | DECIMAL _EXP?
DECIMAL: DIGITS "." DIGITS
    | DIGITS "." /(?!\.)/
    | /(?<!\.)/ "." DIGITS
DIGITS: DIGIT+

%import common.ESCAPED_STRING
%import common._EXP
%import common.SIGNED_INT
%import common.DIGIT
%import common.LETTER

%ignore " "
