start: expr*

expr: value

value: object
    | array
    | range
    | tuple
    | roll
    | FLT
    | INT
    | WORD
    | ESCAPED_STRING
    | VAR

object: "{" [ pair ("," pair)* ] "}"
pair: WORD ":" value

array: "[" [ value ("," value)* ] "]"
range: "[" INT ".." INT "]"
tuple: "(" [ value ("," value)* ] ")"

roll: INT? distribution
distribution: DIE ( INT | array | range | tuple )


// Tokens

DIE.2: /[dD]/
INT.2: ["+"|"-"]? /\d+/ /(\b|(?=[dD]))/
FLT.2: FLOAT
VAR: "$" WORD
WORD: (LETTER | DIGIT | "_")+

%import common.ESCAPED_STRING
%import common.FLOAT
%import common.DIGIT
%import common.LETTER

%ignore " "
