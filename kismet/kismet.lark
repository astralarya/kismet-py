start: value*

value: object
    | array
    | range
    | tuple
    | roll
    | number
    | WORD
    | ESCAPED_STRING
    | VAR

object: "{" [ pair ("," pair)* ] "}"
pair: WORD ":" value

array: "[" [ value ("," value)* ] "]"
range: "[" number ".." number "]"
    | "[" number "..." number "]" -> crange
tuple: "(" [ value ("," value)* ] ")"

roll: number distribution
    | distribution -> roll1
distribution: DIE ( number | array | range | tuple )

number: float | int

float: FLOAT
int: INT

// Tokens

FLOAT.2: ["+"|"-"] UNSIGNED_FLOAT
DIE.2: /[dD]/
INT.2: SIGNED_INT /(\b|(?=[dD]))/
VAR: "$" WORD
WORD: (LETTER | DIGIT | "_")+

UNSIGNED_FLOAT: INT _EXP
    | DECIMAL _EXP?
DECIMAL: DIGITS "." DIGITS
    | DIGITS "." /(?!\.)/
    | /(?<!\.)/ "." DIGITS
DIGITS: DIGIT+

%import common.ESCAPED_STRING
%import common._EXP
%import common.SIGNED_INT
%import common.DIGIT
%import common.LETTER

%ignore " "
